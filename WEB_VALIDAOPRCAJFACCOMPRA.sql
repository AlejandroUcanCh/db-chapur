create or replace PROCEDURE WEB_VALIDAOPRCAJFACCOMPRA (
  OPRCAJ_ IN INTEGER, 
  TIENDA_ IN INTEGER,  
  IMPORTE IN DOUBLE PRECISION,
  OUT_RESULTSET OUT SYS_REFCURSOR
) IS

  tRET VENTAS.CT_RESULTEXITO := VENTAS.CT_RESULTEXITO();
  rRET VENTAS.CR_RESULTEXITO := VENTAS.CR_RESULTEXITO(0, NULL);

  FECHA_ACTUAL DATE;

  CURSOR C_CONSULTA (P_OPERACION INTEGER, P_TIENDA INTEGER) IS
    SELECT VENTAS.PVIDEOPR.IDFAC, VENTAS.PVIDEOPR.FECHA, VENTAS.PVIDEOPR.STATUS, VENTAS.PVIDEOPR.TIPO_FACTURACION, VENTAS.PVIDEOPR.TIPOPR, ROUND(TOTOPR,2) AS TOTAL_OPR 
    FROM VENTAS.PVIDEOPR 
    WHERE VENTAS.PVIDEOPR.CVETDA = P_TIENDA 
      AND OPRCAJ = P_OPERACION;
      
  CURSOR C_VALIDACION (P_OPERACION INTEGER, P_TIENDA INTEGER) IS
    SELECT NVL((SELECT 1
                FROM ventas.pvideopr a, ventas.pvrenpag b, ventas.pvartics c 
                WHERE a.cvetda = P_TIENDA 
                  AND a.oprcaj = P_OPERACION
                  AND a.status = 0 
                  AND a.tipopr = 1 
                  AND c.clase = 132 
                  AND c.descripcion LIKE '%MOTOCICLETA%'
                  AND b.cvetda = a.cvetda 
                  AND b.oprcaj = a.oprcaj 
                  AND c.numart = b.numart 
                  AND ROWNUM = 1), 0)
    FROM ventas.pvideopr;
      
  R_CONSULTA C_CONSULTA%ROWTYPE;

  CURSOR C_VALIDA_BILLETERA(P_OPERACION INTEGER,P_TIENDA INTEGER) IS
    SELECT COUNT(*) 
    FROM ventas.pvpagope 
    WHERE oprcaj = P_OPERACION 
      AND cvetda = P_TIENDA 
      AND TIPPAG = 8 
      AND SBTPAG = 1 
      AND TPAGAR > 0;

  DIFERENCIA DOUBLE PRECISION;
  NOVALIDO INTEGER;
  VALIDACION INTEGER;
    
BEGIN

  FECHA_ACTUAL := NULL;
  DIFERENCIA := 0.0;

  SELECT TRUNC(SYSDATE)
    INTO FECHA_ACTUAL
  FROM DUAL;

  OPEN C_CONSULTA(OPRCAJ_,TIENDA_);

    FETCH C_CONSULTA INTO R_CONSULTA;
    
    IF C_CONSULTA%NOTFOUND THEN
      rRET.EXITO := 0;
      rRET.MENSAJE := 'LA OPERACION DE CAJA EN LA TIENDA NO EXISTE';
    ELSE
      DIFERENCIA := R_CONSULTA.TOTAL_OPR - IMPORTE;
      IF R_CONSULTA.IDFAC > 0 AND R_CONSULTA.TIPO_FACTURACION = 1 THEN
        rRET.EXITO := -1;
        rRET.MENSAJE := 'LA OPERACION DE CAJA YA SE ENCUENTRA FACTURADA';
      ELSIF R_CONSULTA.STATUS = 1 THEN 
        rRET.EXITO := 0;
        rRET.MENSAJE := 'LA OPERACION DE CAJA SE ENCUENTRA CANCELADA';
      ELSIF R_CONSULTA.TIPOPR <> 1 THEN
        rRET.EXITO := 2;
        rRET.MENSAJE := 'LA OPERACION DE CAJA NO ES UNA OPERACION DE COMPRA';
      ELSIF R_CONSULTA.TIPO_FACTURACION = 2 THEN
        rRET.EXITO := 0;
        rRET.MENSAJE := 'LA OPERACION DE CAJA SE ENCUENTRA FACTURADA GLOBALMENTE';
      ELSIF TO_CHAR(FECHA_ACTUAL,'YYYY') <> TO_CHAR(R_CONSULTA.FECHA,'YYYY') THEN
        rRET.EXITO := 0;
        rRET.MENSAJE := 'LA OPERACION DE CAJA NO ES DEL AÂ¿ ACTUAL';
      ELSIF TO_CHAR(FECHA_ACTUAL,'MM') <> TO_CHAR(R_CONSULTA.FECHA,'MM') THEN
        rRET.EXITO := 0;
        rRET.MENSAJE := 'LA OPERACION DE CAJA NO ES DEL MES ACTUAL';
      ELSIF DIFERENCIA > 1 OR DIFERENCIA < -1 THEN 
        rRET.EXITO := 0;
        rRET.MENSAJE := 'EL IMPORTE NO CORRESPONDE A LA TIENDA - OPERACION';
      ELSE
        OPEN C_VALIDA_BILLETERA(OPRCAJ_,TIENDA_);
          FETCH C_VALIDA_BILLETERA INTO NOVALIDO;
        CLOSE C_VALIDA_BILLETERA;
        
        IF NOVALIDO > 0 THEN
          rRET.EXITO := 3;
          rRET.MENSAJE := 'LA OPERACION PUEDE SER FACTURADA, PERO SOLAMENTE CON RFC GENERICO';
        ELSE
          rRET.EXITO := 1;
          rRET.MENSAJE := 'LA OPERACION PUEDE SER FACTURADA';
        END IF;
      END IF;
    END IF;

    OPEN C_VALIDACION(OPRCAJ_, TIENDA_);
      FETCH C_VALIDACION INTO VALIDACION;
    CLOSE C_VALIDACION;
    
    IF VALIDACION = 1 THEN
      rRET.EXITO := -4;
      rRET.MENSAJE := 'LA OPERACION CONTIENE UN VEHICULO AUTOMOTOR, DEBE SER FACTURADA EN EL MODULO DE SERVICIO AL CLIENTE POR DOCUMENTACION LEGAL REQUERIDA';
    END IF;

  CLOSE C_CONSULTA;
    
  tRET.EXTEND;
  tRET(tRET.LAST) := rRET;
    
  OPEN OUT_RESULTSET FOR
    SELECT 
      T.EXITO,
      T.MENSAJE
    FROM TABLE (CAST(TRET AS VENTAS.CT_RESULTEXITO)) T;

END;
